group = 'org.apereo.portal'
version = '0.9.0-SNAPSHOT'
description = 'Apereo Soffit'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'checkstyle'

defaultTasks 'clean', 'build'

repositories {
    mavenLocal()
    mavenCentral()
}

checkstyle {
    // Stay on checkstyle version 6 until Java version 7 support can be dropped
    toolVersion = '6.19'
    showViolations = true
    configFile = file("${project.rootDir}/checkstyle.xml")
}

configurations {
    /*
     * Defines a 'provided' scope similar to the one in Maven.  Dependencies
     * defined using provided will not become transitive dependencies.
     */
    provided

    /*
     * Makes provided dependencies available on the compile classpath in a way
     * that works both from the command line and in Buildship.
     */
    compile.extendsFrom provided

    /*
     * Configure checkstyle
     */
    checkstyle
}

dependencies {

    compile ("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}")
    compile("commons-io:commons-io:${commonsIoVersion}")
    compile("io.jsonwebtoken:jjwt:${jjwtVersion}")
    compile("javax.servlet:jstl:${jstlVersion}")
    compile("net.sf.ehcache:ehcache-core:${ehcacheVersion}")
    compile("org.apache.commons:commons-lang3:${commonsLang3Version}")
    compile("org.apache.httpcomponents:httpclient:${httpclientVersion}")
    compile("org.jasypt:jasypt:${jasyptVersion}")
    compile group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"
    compile group: 'org.springframework', name: 'spring-web', version: "${springVersion}"
    compile group: 'org.springframework', name: 'spring-webmvc-portlet', version: "${springVersion}"

    provided group: 'javax.servlet', name: 'servlet-api', version: "${servletVersion}"
    provided group: 'org.apache.portals', name: 'portlet-api_2.1.0_spec', version: '1.0'

}
